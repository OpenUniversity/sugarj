package template;

import org.sugarj.languages.Stratego;

import template.Core;
import template.ConcreteSyntax;
import template.util.MetaExplode;

public sugar UnquoteSplicing { 
  context-free syntax
    UnquoteSplicing -> JavaImportDec
    UnquoteSplicing -> JavaClassBodyDec
    UnquoteSplicing -> JavaBlockStm
    UnquoteSplicing -> JavaFormalParam

  context-free syntax
    "$*{" UnquoteSplice "}" -> UnquoteSplicing {cons("FromMetaExpr")}
    StrategoStrategy -> UnquoteSplice {cons("UnquoteSplice")}

  desugarings
    list-unquote-desugar

  rules
    list-unquote-desugar :
      TransBody([Rules([SDefNoArgs("main", body)])]) -> 
      TransBody([Rules([SDefNoArgs("main", Seq(Match(Var(modelvar)), body'))])])
    where
      <oncetd(?UnquoteSplice(_))> body;
      modelvar := "this__";
      <topdown(try(list-unquoted-app(|modelvar)))> body => body'

    list-unquoted-app(|modelvar) :
      Op("Cons",[UnquoteSplice(strat), xs]) ->
      App(CallNoArgs(SVar("conc")), NoAnnoList(Tuple([strat-app, xs])))
    where strat-app := App(strat, Var(modelvar))
}