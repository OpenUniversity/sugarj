package template;

import org.sugarj.languages.Stratego;

import template.Core;
import template.ConcreteSyntax;
import template.util.MetaExplode;

public sugar Unquote { 
  context-free syntax
    Unquote -> JavaPackageDec
    Unquote -> JavaPackageName
    Unquote -> JavaImportDec

    "typename" Unquote -> JavaTypeName
    "id" Unquote -> JavaId

    Unquote -> JavaTypeName
    Unquote -> JavaId {avoid}

    Unquote -> JavaBlockStm
    Unquote -> JavaExpr

  context-free syntax
    "${" Unquoted "}" -> Unquote {cons("FromMetaExpr")}
    StrategoStrategy -> Unquoted {cons("Unquoted")}

  desugarings
    unquote-desugar

  rules
    unquote-desugar :
      SDefNoArgs("main", body) -> 
      SDefNoArgs("main", Seq(Match(Var(modelvar)), body'))
    where
      <oncetd(?Unquoted(_))> body;
      modelvar := "this__";
      <topdown(try(unquoted-app(|modelvar)))> body => body'

    unquoted-app(|modelvar) : Unquoted(strat) -> App(strat, Var(modelvar))
}