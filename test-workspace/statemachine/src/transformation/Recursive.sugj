package transformation;

import transformation.Renaming;

public transformation Recursive {
rules
  main =
    ?(model, model-path, trans-path);
    !(model-path, trans-path);
    prim("SUGARJ_rename_main_args") => (old-name, new-name);
    where(!SDefNoArgs(new-name, Call(SVar("recursively-transform"), [Id(), CallNoArgs(SVar(old-name))])) => new-main);
    !model;
    SugarCompilationUnit(
      id,
      ensure-import(|TypeImportDec(TypeName(PackageOrTypeName(Id("transformation")), Id("Recursive")))),
      map(SugarDec(id, SugarBody(!([transformation-elem(Rules([new-main]))], <id>); conc))));
    prim("SUGARJ_compile", model-path, trans-path)

  recursively-transform(model-predicate, trans) = ?(model, model-path, trans-path); 
    {| Renamings, Stack: 
         rules ( Renamings := [] 
                 Stack := [model-path]);
         !model;
         (where(model-predicate) < trans + id) => generated;
         recursively-transform(model-predicate, trans | trans-path);
         compile-renamed(model-predicate | model-path, trans-path)
    |}

  recursively-transform(model-predicate, trans | trans-path) =
    topdown(try(where(
      {pkgs, name, model-path, model, generated, new-name, new-renaming, old-renamings, new-renamings:
        get-imported-model(model-predicate) => (pkgs, name, model-path, model);
        where(!model-path;debug(!"check import: "));
        where(<conc-strings> (name, <name-suffix> trans-path) => new-name;
              !(pkgs, name, new-name) => new-renaming);

        where(old-renamings := <Renamings>;
              new-renamings := [ new-renaming | old-renamings ]);

        (not(is-circular-import(|<debug(!"test circularity: ")> model-path));
         not(is-circular-import(|<compose-path; debug(!"test circularity: ")> (pkgs, new-name)))
         <+ !model-path;debug(!"found circularity: "); rules( Renamings := new-renamings ); fail);

        rules( Stack := <debug(!"Stack push: ")> [ model-path | <Stack> ] );
        finally-do(
          where(!model-path;debug(!"apply trans: "));
          <trans> model => generated;
          where(!model-path;debug(!"trans ok: "));
          rules( Renamings := new-renamings );
          {| Renamings:
            rules ( Renamings := [new-renaming] );
            recursively-transform(model-predicate, trans | trans-path);
            compile-renamed(model-predicate | model-path, trans-path)
          |},
          rules( Stack := <debug(!"Stack pop:  ")><Stack; Tl> ))
      })))

  compile-renamed(model-predicate | model-path, trans-path) =
    where(apply-renamings(|<Renamings; debug(!"rename: ")>) => renamed);
    if has-circular-import(model-predicate)
      then !renamed;
           where(!(model-path, trans-path);debug(!"write renamed: "));
           prim("SUGARJ_write", model-path, trans-path)
      else !renamed;
           where(!(model-path, trans-path);debug(!"compile renamed: "));
           prim("SUGARJ_compile", model-path, trans-path)
    end

  has-circular-import(model-predicate) =
    where(oncetd(is-circular-import(model-predicate)))

  is-circular-import(model-predicate) =
    get-imported-model(model-predicate) => (pkgs, name, model-path, model);
    is-circular-import(|model-path)
  
  is-circular-import(|model-path) =
    <elem> (model-path,<Stack>)

  get-imported-model(predicate) =
    ?TypeImportDec(<id>);
    import-model-path => (pkgs, name);
    <build-path> (pkgs, name) => path;
    prim("SUGARJ_resolve_model") => model;
    where(predicate);
    !(pkgs, name, path, model)

  ensure-import(|imp) = !(<id>, [imp]); union

  conforms-to-metamodel(|meta-model) =
    where(collect-one(?ModelDecHead(_, _, meta-model)))

  finally-do(s, final) = s < final + (final; fail)
}

