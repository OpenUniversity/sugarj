package transformation;

import transformation.Renaming;

public transformation Recursive {
rules
  main = compiled(main)

  compiled(trans) =
    ?(model, model-path, trans-path);
    !model;
    trans;
    prim("SUGARJ_compile", model-path, trans-path)

  main = SugarCompilationUnit(
    id,
    ensure-import(|TypeImportDec(TypeName(PackageOrTypeName(Id("transformation")), Id("Recursive")))),
    map(debug;{name: SugarDec(SugarDecHead(id, Id(?name)), SugarBody(debug;recursify; debug;rename-main-rules(|name);debug))}))
  
  recursify = 
    topdown(try(\ SDefNoArgs(sname, trans) -> 
                  SDefNoArgs("main", Call(SVar("recursively-transform"), [Id(), trans']))
                    where
                      <string-starts-with(|"main-")> sname; 
                      <topdown(try(\ Prim("\"SUGARJ_compile\"", _) -> Id() \))> trans => trans'
                \))

  recursively-transform(model-predicate, trans) = ?(model, model-path, trans-path); 
    {| Renamings: 
         rules ( Renamings := [] );
         !model;
         (where(model-predicate) < trans + id) => generated;
         recursively-transform(model-predicate, trans | trans-path);
         prim("SUGARJ_compile", model-path, trans-path)
    |}

  recursively-transform(model-predicate, trans | trans-path) =
    topdown(try(where(
      {pkgs, name, model-path, model, generated:
        get-imported-model(model-predicate) => (pkgs, name, model-path, model);
        <trans> model => generated;
        rules( Renamings := [ (pkgs, name, (<conc-strings> (name, <name-suffix> trans-path)))
                            | <Renamings>]);
        where(recursively-transform(model-predicate, trans | trans-path));
        prim("SUGARJ_compile", model-path, trans-path)
      })));
    apply-renamings(|<Renamings>)
  
  get-imported-model(predicate) =
    ?TypeImportDec(<id>);
    import-model-path => (pkgs, name);
    <build-path> (pkgs, name) => path;
    prim("SUGARJ_resolve_model") => model;
    where(predicate);
    !(pkgs, name, path, model)

  ensure-import(|imp) = !(<id>, [imp]); union

  conforms-to-metamodel(|meta-model) =
    collect-one(?ModelBody((meta-model, _)))
}

