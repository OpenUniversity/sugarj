module org/sugarj/stdlib/Common

signature
  constructors
    Locked : a -> a

rules
  changed-tree(s) = ?t; s; not(equal(|t))

rules
  intersect =
      ?([],_)
    ; ![]
  intersect =
      ?(_,[])
    ; ![]
  intersect =
      ?([x|xs],ys)
    ; <intersect> (xs, ys) => rest
    ; if <elem> (x, ys)
        then ![x|rest]
        else !rest
      end

  /* (xs:List[a], ys:List[a]) -> true/false
   * Checks if xs contains all elements of ys.
   */
  contains-all =
      ?(xs, ys)
    ; Swap
    ; intersect
    ; equal(|ys)

  foldl1(s) =
  	  ?[x|xs]
  	; <foldl(s)> (xs, x)
  
  myfoldr1(one1, cons) = myfoldr1(fail, one1, cons)
  myfoldr1(nil, one1, cons) =
  	  ?[] 
  	  < nil
  	  + ?[<id>]
  	    < one1
  	    + ?[x|xs]; <cons> (x, <myfoldr1(nil, one1, cons)> xs)
